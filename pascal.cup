import java_cup.runtime.*;
import java.util.*;
import java.io.*;

parser code {:
  public static void main(String args[]) throws Exception {
    new Parser(new Lexer(new InputStreamReader(System.in))).parse();
  }
:}

action code {:
  // attributes & methods seen from actions

  String make_inst(String lab, String op, String arg) {
    return lab + "\t" + op + "\t" + arg + "\n";
  }

  String make_comm(String comment) {
    return ";/ " + comment + "\n";
  }
:}

terminal TK_SEP, TK_PRINT, TK_ADD, TK_MINUS, TK_PARENTHESE_L, TK_PARENTHESE_R, TK_MULTIPLE, TK_DIVIDE;
terminal Integer TK_ICNST;
terminal String TK_ILETTER;

non terminal target;
non terminal String program, linst, inst, print, expr;

precedence left TK_ADD, TK_MINUS;
precedence left TK_MULTIPLE, TK_DIVIDE;
precedence left TK_PARENTHESE_L, TK_PARENTHESE_R;

start with target;

target ::= program:s {: System.out.println(s); :}
  ;

program ::= linst:l {: RESULT = l + "\tSTOP\n"; :}
  ;
linst ::= inst:i {: RESULT = i; :}
  | linst:l inst:i {: RESULT = l + i; :}
  ;
inst  ::= print:i TK_SEP {: RESULT = i; :}
	| TK_SEP {: RESULT = ""; :}
	;
print ::= TK_PRINT expr:e
        {:
	    String comment = make_comm("print...");
	    RESULT = comment + e + "\tOUT\n";
	:}
	;
expr ::= TK_ICNST:i {: RESULT = "\tPUSH\t" + i + "\n"; :}
  | TK_ILETTER:l {: RESULT = "\tPUSH\t" + l + "\n"; :}
  | expr:e1 TK_ADD expr:e2 {: RESULT = e1 + e2 + "\tADD\n"; :}
  | expr:e1 TK_MINUS expr:e2 {: RESULT = e1 + e2 + "\tSUB\n"; :}
  | TK_PARENTHESE_L expr:e TK_PARENTHESE_R {: RESULT = e; :}
  | expr:e1 TK_MULTIPLE expr:e2 {: RESULT = e1 + e2 + "\tMUL\n"; :}
  | expr:e1 TK_DIVIDE expr:e2 {: RESULT = e1 + e2 + "\tDIV\n"; :}
	;
